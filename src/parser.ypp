%{
#define YYDEBUG 1
#include <stdio.h>
#include <string>
#include <iostream>
#include <fstream>

#include "../src/Memory.hpp"
#include "../src/Variable.hpp"
#include "../src/Constant.hpp"
#include "../src/Value.hpp"
#include "../src/Code.hpp"

using namespace std;

int yylex();
int yyerror(string);
extern FILE *yyin;
extern int yylineno;

Code* code;
Memory* memory;

%}

%union token_val {
    string* pid;
    long long num;
    Variable* var;
    Value* val;
}

%start program

%token DECLARE T_BEGIN END
%token IF WHILE DO
%token FOR 
%token THEN ELSE ENDIF FROM TO DOWNTO ENDFOR ENDWHILE ENDDO
%token READ WRITE       
%token LE GE LEQ GEQ EQ NEQ
%token ASSIGN
%token ERROR

%token <pid> PID
%token <num> NUM

%type <var> identifier
%type <val> value

%left MINUS PLUS
%left TIMES DIV MOD
%token SEMICOLON COLON COMMA LPAREN RPAREN



%%
program: DECLARE declarations T_BEGIN commands END
       | T_BEGIN commands END
       ;

declarations: declarations COMMA declaration 
            | declarations COMMA arr_declaration
            | declaration
            | arr_declaration
            ;

declaration: PID { memory->reserve_variable(*$1); }
           ;

arr_declaration: PID LPAREN NUM COLON NUM RPAREN { memory->reserve_array(*$1, $3, $5); }
               ;

commands: commands command
        | command
        ;

command: identifier ASSIGN expression SEMICOLON 
       | IF condition THEN commands ELSE commands ENDIF
       | IF condition THEN commands ENDIF
       | WHILE condition DO commands ENDWHILE
       | DO commands WHILE condition ENDDO
       | FOR PID FROM value TO value DO commands ENDFOR
       | READ identifier SEMICOLON                       { code->read($2); }
       | WRITE value SEMICOLON                           { code->write($2); }
       ;

expression: value
          | value PLUS value
          | value MINUS value
          | value TIMES value
          | value DIV value
          | value MOD value
          ;

condition: value EQ value
         | value NEQ value
         | value LE value
         | value GE value
         | value LEQ value
         | value GEQ value
         ;

value: NUM          { $$ = new Constant($1); }
     | identifier   { $$ = $1; }
     ;

identifier: PID                   {
                                    $$ = memory->get_variable(*$1);  
                                  }
          | PID LPAREN PID RPAREN {
                                    $$ = memory->get_variable(*$1, *$3);
                                  }
          | PID LPAREN NUM RPAREN {
                                    $$ = memory->get_variable(*$1, $3);
                                  }
          ;

%%

int yyerror(string s)
{
    cout << "\033[1;31m" 
         << "Error in line " << yylineno << ": " 
         << "\033[0m"
         << s << endl;
    return 0;
}

int main(int argc, char* argv[])
{
    // yydebug = 1;
    if (argc != 3)
    {
        cout << "Not enough arguments" << argc << endl;
        return 1;
    }

    memory = new Memory();
    code = new Code(memory);

    yyin = fopen(argv[1], "r");
    try
    {
        yyparse();
        ofstream output;
        output.open(argv[2]);
        output << code->get_code();
        output.close();
    }
    catch (string error)
    {
        yyerror(error);
    }

    return 0;
}
